import groovy.xml.MarkupBuilder

apply plugin: 'java'

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  compile project(':github-pr')
  compile project(':github-pr-status')
  compile project(':github-oauth-authorization-plugin')
}

version = '1.0.0'

project.ext.gocdPlugins = [
  [
    id              : 'cd.go.authorization.github',
    name            : 'GitHub OAuth authorization plugin',
    pluginVersion   : project.version,
    gocdVersion     : '19.11.0',
    description     : 'GitHub OAuth authorization plugin for GoCD',
    vendorName      : 'ThoughtWorks, Inc.',
    vendorUrl       : 'https://github.com/gocd-contrib/github-oauth-authorization-plugin',
    extensionClasses: ["cd.go.authorization.github.GitHubPlugin"]
  ],
  [
    id              : 'github.pr',
    name            : 'Github Pull Requests Builder',
    pluginVersion   : project.version,
    gocdVersion     : '19.11.0',
    description     : 'Plugin that polls a GitHub repository for pull requests and triggers a build for each of them',
    vendorName      : 'Ashwanth Kumar',
    vendorUrl       : 'https://github.com/ashwanthkumar/gocd-build-github-pull-requests',
    extensionClasses: ["in.ashwanthkumar.gocd.github.GitHubPRBuildPlugin"]
  ],
  [
    id              : 'github.pr.status',
    name            : 'Github Pull Requests status notifier',
    pluginVersion   : project.version,
    gocdVersion     : '19.11.0',
    description     : 'Updates build status for GitHub pull request',
    vendorName      : 'GoCD Contributors',
    vendorUrl       : 'https://github.com/gocd-contrib/gocd-build-status-notifier',
    extensionClasses: ["com.tw.go.plugin.GitHubBuildStatusNotifierPlugin"]
  ]
]

task generateBundleXML() {
  def bundleXmlFile = file("gocd-bundle.xml")
  outputs.file(bundleXmlFile)

  bundleXmlFile.withWriter { writer ->
    def xml = new MarkupBuilder(new IndentPrinter(writer, "    ", true))
    xml.doubleQuotes = true
    xml.mkp.xmlDeclaration(version: '1.0', encoding: 'utf-8')

    xml."gocd-bundle"(version: "1") {
      xml.plugins {
        project.gocdPlugins.forEach { plugin ->
          xml.plugin(id: plugin.id) {
            xml.about {
              xml.name(plugin.name)
              xml.version(plugin.pluginVersion)
              xml.description(plugin.description)
              xml."target-go-version"(plugin.gocdVersion)
              xml.vendor {
                name(plugin.vendorName)
                url(plugin.vendorUrl)
              }
            }
            xml.extensions {
              plugin.extensionClasses.each { clazz ->
                xml.extension(class: clazz)
              }
            }
          }
        }
      }
    }
  }
}

jar {
  from(generateBundleXML.outputs) {
    into "/"
  }
  archiveBaseName = "gocd-github-plugins-bundle"
  from(configurations.compile) {
    into "lib/"
  }

  from(sourceSets.main.java) {
    into "/"
  }
}

subprojects {
  apply plugin: 'java'
  apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/test-task-helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '19.10.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compile group: 'org.kohsuke', name: 'github-api', version: '1.99'
    compile group: 'in.ashwanthkumar', name: 'git-cmd', version: '1.4'
    compile group: 'in.ashwanthkumar', name: 'my-java-utils', version: '0.0.6'

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.10'

    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.2.2'

    testCompile group: 'cd.go.plugin', name: 'go-plugin-api', version: '19.10.0'
    testCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.2.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.0.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testCompile group: 'org.jsoup', name: 'jsoup', version: '1.12.1'
  }
}

